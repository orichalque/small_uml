grammar org.smalluml.SmallUml with org.eclipse.xtext.common.Terminals

import "platform:/resource/SmallUML/model/smalluml.ecore"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Package returns Package:
	{Package}
	'Package'
	name=EString
	BEGIN
		(member+=NamedElement)* 
	END;

NamedElement returns NamedElement:
	Class | Attribute | Method | Parameter | Role | Composition | Reference | Package;

Type returns Type:
	String0 | Integer | Bool | Real | UnlimitedNatural | Enumeration;

EString returns ecore::EString:
	STRING | ID;

Class returns Class:
	{Class}
	'Class' 
	name=EString ('extends' super=[Class|EString])?	
	(
	BEGIN
	((method+=Method)|(attribute+=Attribute))*		
	END
	)?		

;



Attribute returns Attribute:	
	'+' name=EString ':' type=Type
;

Method returns Method:
	{Method}	
	'+'name=EString'('
		(parameter+=Parameter ( "," parameter+=Parameter)*)?
	')'
		(':' returnType=Type)?
;

Parameter returns Parameter:
	name=EString ':' type=Type
;

Composition returns Composition:
	(name=EString ':')?source=Role'<>--'target=Role		
;

Reference returns Reference:	
	(name=EString ':')?source=Role'--->'target=Role
;

Role returns Role:
	(name=EString)?'('class=[Class|EString]
		(',''['lowerBound=EInt ',' upperBound=EInt ']')?
	')';

String0 returns String:
	{String}
	'String'
	;

Integer returns Integer:
	{Integer}
	'Integer'
	;

Bool returns Bool:
	{Bool}
	'Bool'
	;

Real returns Real:
	{Real}
	'Real'
	;

UnlimitedNatural returns UnlimitedNatural:
	{UnlimitedNatural}
	'UnlimitedNatural'
	;

Enumeration returns Enumeration:
	'Enumeration'
	'{'
		'value' '{' value+=String0 ( "," value+=String0)* '}' 
	'}';

EInt returns ecore::EInt:
	'-'? INT;


terminal BEGIN: 'synthetic:BEGIN';  // increase indentation
terminal END: 'synthetic:END'; // decrease indentation	
